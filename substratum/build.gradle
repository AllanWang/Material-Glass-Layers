apply plugin: 'com.android.application'


android {
    compileSdkVersion Integer.parseInt(project.TARGET_SDK)
    buildToolsVersion project.BUILD_TOOLS

    defaultConfig {
        applicationId project.APP_PACKAGE
        minSdkVersion Integer.parseInt(project.MIN_SDK)
        targetSdkVersion Integer.parseInt(project.TARGET_SDK)
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName project.VERSION_NAME
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent,
                    "${APP_TAG_SHORT}-${variant.productFlavors[1].name}.apk")
        }
    }

    signingConfigs {
        test {
            storeFile file("../test.keystore")
            storePassword "testkey"
            keyAlias "testKey"
            keyPassword "testkey"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions("delta", "base")
    productFlavors {

        //deltas contain components that are wip
        Clear_delta {
            dimension "delta"
        }
        Dark_delta {
            dimension "delta"
        }

        //base contains actual build flavours
        Clear_sample {
            dimension "base"
            resValue "string", "app_name", "MGlass Clear Test"
            signingConfig signingConfigs.test
        }
        Clear_full {
            dimension "base"
            resValue "string", "app_name", "MGlass Clear"
            signingConfig signingConfigs.test
        }
        Dark_sample {
            dimension "base"
            resValue "string", "app_name", "MGlass Dark Test"
            signingConfig signingConfigs.test
        }
        Dark_full {
            dimension "base"
            resValue "string", "app_name", "MGlass Dark"
            signingConfig signingConfigs.test
        }
    }

    variantFilter { variant ->
        def delta = variant.getFlavors().get(0).name
        def base = variant.getFlavors().get(1).name
        def theme = base.split('_')[0]
        if (!delta.startsWith(theme)) {
            variant.setIgnore(true);
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
//    testCompile 'junit:junit:4.12'
//    compile "com.android.support:appcompat-v7:$project.ANDROID_SUPPORT_LIBS"
//    compile "com.android.support:support-v4:$project.ANDROID_SUPPORT_LIBS"
    compile project(':substrate')
}
